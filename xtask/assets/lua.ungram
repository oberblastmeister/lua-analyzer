Name =
  'ident'

NameRef =
  'ident'

MultiName =
  Name (',' Name)*

SourceFile =
  body:Block

Stmt =
  AssignStmt
  | LocalAssignStmt
  | LocalFunctionDefStmt
  | FunctionDefStmt
  | ForStmt
  | IfStmt
  | ReturnStmt
  | WhileStmt
  | ExprStmt
  | BreakStmt
  | RepeatUntilStmt
  | LabelStmt
  | GotoStmt

StmtItem =
  LocalAssignStmt
  | LocalFunctionDefStmt
  | FunctionDefStmt

LabelStmt =
  start:LabelDelim Name finish:LabelDelim

LabelDelim =
  '::'

GotoStmt =
  'goto' NameRef

BreakStmt =
  'break'

DoStmt =
  'do'
  body:Block
  'end'

FunctionDefStmt =
  'function' 
  FunctionDefContent
  ParamList
  body:Block
  'end'

FunctionDefContent =
  FunctionMethod
  | FunctionStatic
  | Name

FunctionMethod =
  IndexPath
  ':'
  Name

FunctionStatic =
  IndexPath
  '.'
  Name

LocalFunctionDefStmt =
  'local' 'function' Name ParamList
  body:Block
  'end'

ParamList =
  '(' (MultiName (',' '...')?)? ')'

ExprStmt = MultivalExpr

IfStmt =
  'if'
  cond:Expr
  'then'
  Block
  (ElseBranch | ElseIfBranch)?
  'end'

ElseBranch =
  'else'
  Block

ElseIfBranch =
  'elseif'
  cond:Expr
  'then'
  Block
  (ElseIfBranch | ElseBranch)?

WhileStmt =
  'while'
  cond:Expr
  'do'
  body:Block
  'end'

ReturnStmt =
  'return'
  Expr?

ForStmt =
  'for' content:ForContent 'do' Block 'end'

ForContent =
  NumericFor
  | GenericFor

NumericFor =
  Name
  '='
  expr:MultivalExpr

GenericFor =
  multi_name:MultiName
  'in'
  expr:MultivalExpr
  
Block =
  Stmt*

RepeatUntilStmt =
  'repeat'
  Block
  'until'
  Expr
  
LocalAssignStmt =
  'local' MultiName ('=' MultivalExpr)?

AssignStmt =
  lhs:MultivalExpr ('=' rhs:MultivalExpr)?

Expr =
  Literal
  | TableExpr
  | InfixExpr
  | PrefixExpr
  | IndexExpr
  | DotExpr
  | FunctionExpr
  | CallExpr
  | MethodCallExpr
  | NameRef

CallExpr =
  fun:Expr CallArgs

MethodCallExpr =
  fun:Expr ':' NameRef CallArgs

MultivalExpr =
  Expr (',' Expr)*

CallArgs =
  ArgList
  | TableExpr
  | 'str'

ArgList =
  '(' args:(Expr (',' Expr)*)? (',' '...')? ')'

Literal =
  'number'
  | 'str'
  | 'true'
  | 'false'

FunctionExpr =
  'function'
  params:ParamList
  body:Block
  'end'

TableExpr =
  '{'
  (TableContent (TableSep TableContent)* TableSep?)
  '}'

TableSep =
  ',' | ';'

DotExpr =
  Expr '.' 'ident'

ParenExpr =
  '('
  Expr
  ')'

TableContent =
  KeyValue
  | PositionalValue

PositionalValue =
  Expr

KeyValue =
  key:TableKey value:Expr
  
TableKey =
  Index
  | Name

IdentKey =
  'ident'

InfixExpr =
  lhs:Expr
  op:('^' | '*' | '/' | '+' | '-' | '..' | '<' | '>' | '<=' | '>=' | '~=' | '==' | 'and' | 'or')
  rhs:Expr

PrefixExpr =
  op:('not' | '-') Expr

IndexExpr =
  base:Expr Index

Index =
  '[' Expr ']'

IndexPath =
  NameRef ('.' NameRef)*

// vim: set shiftwidth=2
